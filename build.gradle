plugins {
	id 'java'
	id 'jacoco'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id "org.sonarqube" version '4.4.1.3373'
	id "org.flywaydb.flyway" version "10.15.2"
	id "com.diffplug.spotless" version "6.21.0"
}

group = 'com.quemistry'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}

	testCompile {
		/* only junit 5 should be used */
		exclude group: 'junit', module: 'junit'
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testRuntime {
		/* only junit 5 should be used */
		exclude group: 'junit', module: 'junit'
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

}


repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.1.3'
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'

	implementation 'org.flywaydb:flyway-database-postgresql'
	implementation 'org.flywaydb:flyway-core'

	implementation 'org.javatuples:javatuples:1.2'

	compileOnly 'org.projectlombok:lombok'

	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

	runtimeOnly "org.springframework.boot:spring-boot-starter-actuator"
	runtimeOnly 'org.postgresql:postgresql'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-api'
	testImplementation 'com.h2database:h2'
	testImplementation 'com.github.jsonzou:jmockdata:4.3.0'

	testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test

	reports {
		xml.required = true
	}

	afterEvaluate {
		excludedClassFilesForReport(classDirectories)

	}
}

private excludedClassFilesForReport(classDirectories) {
	classDirectories.setFrom(files(classDirectories.files.collect {
		fileTree(dir: it, exclude: [
				'**/entity/*.class',
				'**/model/**/*.class',
				'**/model/*.class',
				'**/entity/**/*.class',
				'**/Application.class',
				'**/constant/**/*.class',
				'**/constant/*.class',
				'com/quemistry/quiz_ms/exception/ExceptionResponse.class'
		])
	}))
}

bootJar {
	archiveFileName = "quiz_ms.jar"
	archiveClassifier.set(null)
}

tasks.register('installLocalGitHook', Copy) {
	from new File(rootProject.rootDir, 'scripts/pre-commit')
	into { new File(rootProject.rootDir, '.git/hooks') }
	fileMode 0775
}

build {
	finalizedBy("installLocalGitHook")
}

sonar {
	properties {
		property "sonar.projectKey", "mtech-31-quemistry_quiz_ms"
		property "sonar.organization", "mtech-31-quemistry"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

spotless {
	java {
		googleJavaFormat() // 使用 Google Java 格式化程序
	}
}
